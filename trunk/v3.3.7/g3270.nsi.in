!include "MUI.nsh"

Name "@PACKAGE_NAME@"
Caption "@PACKAGE_NAME@ - 3270 Emulator for windows/gtk"
outfile "@PACKAGE_NAME@-@PACKAGE_VERSION@-setup.exe"
XPStyle on

# define the directory to install to
installDir $PROGRAMFILES\@PACKAGE_NAME@

# Get installation folder from registry if available
InstallDirRegKey HKCU "Software\@PACKAGE_NAME@" ""

RequestExecutionLevel admin

# Interface

!define MUI_ABORTWARNING
# !insertmacro MUI_PAGE_WELCOME
# !insertmacro MUI_PAGE_LICENSE "COPYING"
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

# !insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
# !insertmacro MUI_UNPAGE_FINISH

# Languages
!insertmacro MUI_LANGUAGE "English"

# default section
Section "@PACKAGE_NAME@" SecApp

	# define the output path for this file
	setOutPath $INSTDIR

	createShortCut "$SMPROGRAMS\@PACKAGE_NAME@.lnk" "$INSTDIR\@PACKAGE_NAME@.exe"
	createShortCut "$DESKTOP\@PACKAGE_NAME@.lnk" "$INSTDIR\@PACKAGE_NAME@.exe"

	# Binary files
	file bin\Release\@PACKAGE_NAME@.exe
	file bin\Release\lib3270.dll
	file bin\Release\w3n46.dll

	# UI definition files
	file src\g3270\actions.conf
	file src\g3270\ui.xml
	file "/oname=$INSTDIR\@PACKAGE_NAME@.jpg" src\g3270\@PACKAGE_NAME@.jpg 
	file "/oname=$INSTDIR\@PACKAGE_NAME@.ico" src\g3270\@PACKAGE_NAME@.ico

	# Locale files
	CreateDirectory "$INSTDIR\@localedir@\pt_BR\LC_MESSAGES"
	file "/oname=$INSTDIR\locale\pt_BR\LC_MESSAGES\@PACKAGE_NAME@.mo" "@localedir@\pt_BR\LC_MESSAGES\@PACKAGE_NAME@.mo"

	# define uninstaller name
	writeUninstaller $INSTDIR\uninstall.exe

	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PACKAGE_NAME@" \
	                 "DisplayName" "@PACKAGE_NAME@ - 3270 emulator for windows/gtk"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PACKAGE_NAME@" \
	                 "UninstallString" "$INSTDIR\uninstall.exe"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PACKAGE_NAME@" \
	                 "InstallLocation" "$INSTDIR"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PACKAGE_NAME@" \
	                 "NoModify" "1"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PACKAGE_NAME@" \
	                 "NoRepair" "1"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PACKAGE_NAME@" \
	                 "DisplayVersion" "@PACKAGE_VERSION@"

	# Save instalation dir
	WriteRegStr HKCU "Software\@PACKAGE_NAME@" "" $INSTDIR

sectionEnd

Section "GTK+ Runtime" SecGTK

	setOutPath $INSTDIR
	file "@GTKROOT@\lib\*.*"

	setOutPath $INSTDIR\lib\gtk-2.0
	file /r "@GTKROOT@\lib\gtk-2.0\*.*"

	SetOutPath $INSTDIR\share
	file /r @GTKROOT@\share\*.*

	SetOutPath $INSTDIR\etc
	file /r "@GTKROOT@\etc\*.*"

	SetOutPath $INSTDIR
	file /r "@GTKROOT@\bin\*.*"

	SetOutPath $INSTDIR\gtk2-runtime
	file /r "@GTKROOT@\gtk2-runtime\*.*"

sectionEnd

# create a section to define what the uninstaller does.
# the section will always be named "Uninstall"
section "Uninstall"

	# Always delete uninstaller first
	delete $INSTDIR\uninstaller.exe

	# now delete installed files
	delete $INSTDIR\@PACKAGE_NAME@.exe
	delete $INSTDIR\lib3270.dll
	delete $INSTDIR\@PACKAGE_NAME@.conf
	delete $INSTDIR\actions.conf
	delete $INSTDIR\w3n46.dll
	delete $INSTDIR\ui.xml
	delete $INSTDIR\@PACKAGE_NAME@.jpg
	delete $INSTDIR\@PACKAGE_NAME@.ico
	delete $INSTDIR\@PACKAGE_NAME@.log
	delete "$SMPROGRAMS\@PACKAGE_NAME@.lnk"
	delete "$DESKTOP\@PACKAGE_NAME@.lnk"

	RMDir /r "$INSTDIR\locale"
	RMDir /r "$INSTDIR\share"
	RMDir /r "$INSTDIR\etc"
	RMDir /r "$INSTDIR\gtk2-runtime"

	# Delete all files
	delete "$INSTDIR\*.dll"

	# Remove registry
	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@PACKAGE_NAME@"

	RMDir /r "$INSTDIR"

sectionEnd
